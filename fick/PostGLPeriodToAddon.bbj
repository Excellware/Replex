REM /**
REM  * PostGLPeriodToAddon.bbj
REM  * @author Len Krause, Excellware, Inc.
REM  * March 2020
REM  *
REM  * One Time run to post closed periods 1 thru 10 to Addon 
REM  *
REM **/

tmp$ = stbl("!ERROR_HANDLER", "/u/CDI/CD/bbj/CDS063.bbj")
REM setopts $080872202c2e$

rem  Determine latest copy of Addon data, Addon retains prior file versions until manually removed
pipe = unt
open (pipe)"|ls -d /u/addon/v?? | tail -1"
read (pipe)path$
close (pipe)
datapath$ = path$ + "/data/"; rem  /u/addon/v19/data/

open (pipe)"|ls -d " + path$ + "/v* | tail -1"
read (pipe)subpath$
close (pipe)

tpl_dev = unt
open (tpl_dev) subpath$ + "/barista/sys/data/ddm_table_tpls.dat"
read (tpl_dev, key=pad("GLE_JRNLHDR", 16)) *, *, gle01Tpl$
read (tpl_dev, key=pad("GLE_JRNLDET", 16)) *, *, gle11Tpl$
close (tpl_dev)

gl301Tpl$ = "glacctno:c(6*), sub:c(4*), desc:c(40*), fld4:c(4*), dc:c(1*), bb:n(1*), py[12]:n(1*), cy[12]:n(1*)"

dim gl301$:gl301Tpl$
gl301 = unt
open (gl301)"/u/fb/df/GL301"

dim gle01$:gle01Tpl$
gle01 = unt
open (gle01)datapath$ + "gle-01"

dim gle11$:gle11Tpl$
gle11 = unt
open (gle11)datapath$ + "gle-11"

for period = 0 to 10
  
  if period = 0 then
    yyyymmdd$ = fndate$(1)
    yyyymmdd$(7,2)="01"
    journal_id$ = "BB"
    trans_id$   = "BB-" + yyyymmdd$(1,4)
  else
    yyyymmdd$ = fndate$(period)
    journal_id$ = "JE"
    trans_id$   = "Period-" + str(period:"00") 
  fi
  line_no = 0
  
  read (gl301, key="", dom=*next)
  repeat
    readrecord (gl301, end=*break)gl301$

    if period then
      amount = gl301.cy[period]
    else
      amount = gl301.bb
    fi
    
    if amount = 0 continue

    if line_no = 0 then gosub do_header
    
    redim gle11$
    gle11.firm_id$      = "01"
    gle11.journal_id$   = pad(journal_id$, 2)
    gle11.je_trans_id$  = pad(trans_id$, 10)
    line_no = line_no + 1
    gle11.line_no$ = str(line_no:"000")
    gle11.gl_account$ = pad(gl301.glacctno$, 10, "0")
    gle11.gl_post_memo$ = pad("Net Postings from Fick Office", 30)
    if amount >= 0 then
      gle11.debit_amt = amount
      gle11.credit_amt = 0
    else
      gle11.debit_amt = 0
      gle11.credit_amt = -amount
    fi
    gle11.units = 0
    gle11.batch_no$ = fill(7, "0")
    gle11.memo_1024$ = gle11.gl_post_memo$
    writerecord(gle11)gle11$
  until 0
next period
close (gl301)
close (gle01)
close (gle11)
print "Done"
end

  
do_header:
  redim gle01$
  gle01.firm_id$      = "01"
  gle01.journal_id$   = pad(journal_id$, 2)
  gle01.je_trans_id$  = pad(trans_id$, 10)
  gle01.description$  = pad("Net Postings from Fick Office", 30)
  gle01.trans_date$   = yyyymmdd$
  gle01.batch_no$     = fill(7, "0")
  writerecord (gle01)gle01$
return

def fndate$(p_period)
  year = 2019
  mth  = p_period + 3
  if mth > 12 then
    mth = mth - 12
    year = year + 1
  fi
  dae  = 28
  repeat
    jdate = jul(year, mth, dae, err=*break)
    dat$ = date(jdate:"%Yl%Mz%Dz")
    dae = dae + 1
  until 0
  return dat$
fnend

test:
for period = 1 to 12
  print period:"#0B",fndate$(period)
next period